// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'jacoco'

buildscript {

    ext{
        kotlin_version = '1.4.21'
        nav_version = "2.3.4"
        testCoverage = false
    }

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "org.jacoco:org.jacoco.core:0.8.6"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }

    tasks.withType(Test) {
        // copied from https://stackoverflow.com/a/36130467
        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "${project.name} Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
            exceptionFormat = 'full'
            showCauses = true
            showExceptions = true
            showStackTraces = true
        }
        maxParallelForks = 4
        forkEvery=0
    }

}

subprojects{
    println("adding jacoco to " + project)
    apply from: "$project.rootDir/jacoco.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}




