apply plugin: 'java-test-fixtures'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "androidx.navigation.safeargs"

ext{
    androidxtest_version="1.3.0"
    androiedxjunit_version="1.1.2"
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.nepumuk.notizen"
        minSdkVersion 23
        targetSdkVersion 30
        // check version code from system environment (if input from CI-System)
        if ("$System.env.VERSIONCODE"=="null") {
            // not found, check local properties file in root dir of project
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            // default values are also provided, if not in properties file...
            versionCode properties.getProperty('versionCode') as Integer?:1
            versionName properties.getProperty('versionName') as String?:"1.0"
        }else{
            versionCode "$System.env.VERSIONCODE" as Integer
            versionName "$System.env.VERSIONNAME" as String
        }
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "@string/app_name_base"
            testCoverageEnabled rootProject.ext.testCoverage
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "@string/app_name_debug"
            testCoverageEnabled rootProject.ext.testCoverage
        }
        debugminify {
            initWith debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
        }
        releaseminify {
            initWith release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}


dependencies {

    implementation project(":app:core")
    implementation project(":app:tasks")
    implementation project(":app:textnotes")

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.firebase:firebase-appindexing:19.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference-ktx:1.1.1'


    // navigation components
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    // Testing Navigation
    testImplementation "androidx.navigation:navigation-testing:$nav_version"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    // Jetpack Compose Integration
    implementation "androidx.navigation:navigation-compose:1.0.0-alpha09"


    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'


    /**test dependencies**/
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.test:core:$androidxtest_version"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$androidxtest_version"
    androidTestImplementation "androidx.test:rules:$androidxtest_version"
    androidTestImplementation  "androidx.test.ext:junit:$androiedxjunit_version"
    testImplementation  "androidx.test.ext:junit:$androiedxjunit_version"


    testImplementation "org.robolectric:robolectric:4.5-alpha-3" // TODO move to release once released
    /**Power mock**/
    testImplementation "org.powermock:powermock-core:2.0.2"
    testImplementation "org.powermock:powermock-module-junit4:2.0.2"
    testImplementation "org.powermock:powermock-api-mockito2:2.0.2"
    // TODO figure out a better way to test fragments in release build
    implementation "androidx.fragment:fragment-testing:1.2.5"
}
repositories {
    mavenCentral()
}
